;; The first three lines of this file were inserted by DrRacket. They record metadata
;; about the language level of this file in a form that our tools can easily process.
#reader(lib "htdp-intermediate-lambda-reader.ss" "lang")((modname lec19-tree-solution) (read-case-sensitive #t) (teachpacks ()) (htdp-settings #(#t constructor repeating-decimal #f #t none #f () #f)))
(require spd/tags)

(@htdd Tree) 

(define-struct node (name subs))
;; Tree is (make-node String (listof Tree))
;; interp. a bare bones arbitrary arity tree, each node has a name and subs
#;
(define (fn-for-tree t)  
  (local [(define (fn-for-t t)
            (local [(define name (node-name t))  ;unpack the fields
                    (define subs (node-subs t))] ;for convenience
              
              (... name (fn-for-lot subs))))
          
          (define (fn-for-lot lot)
            (cond [(empty? lot) (...)]
                  [else
                   (... (fn-for-t (first lot))
                        (fn-for-lot (rest lot)))]))]
    
    (fn-for-t t)))

(define L1 (make-node "L1" empty))
(define L2 (make-node "L2" empty))
(define L3 (make-node "L3" empty))

(define M1 (make-node "M1" (list L1)))
(define M2 (make-node "M2" (list L2 L3)))

(define TOP (make-node "TOP" (list M1 M2)))


;;
;; PROBLEM
;;
;; Design a function that given a tree and a name tries to find a path to a
;; node with that name.
;;

(@htdf find-path)

(@signature Tree String -> (listof String) or false)
;; produce path to given node name if found
(check-expect (find-path L1 "L1") (list "L1"))
(check-expect (find-path L1 "foo") false)
(check-expect (find-path TOP "L1") (list "TOP" "M1" "L1"))
(check-expect (find-path TOP "L2") (list "TOP" "M2" "L2"))
(check-expect (find-path TOP "L3") (list "TOP" "M2" "L3"))

#;#;
(@template backtracking Tree (listof Tree) accumulator)

;; Structural recursion with path accumulator. Works properly.
(define (find-path t to)
  ;; path is (listof String); names of parent, grandparent... trees to here
  ;;                          (builds along recursive  calls)
  (local [(define (fn-for-t t path)
            (local [(define name (node-name t))
                    (define subs (node-subs t))
                    (define npath (append path (list name)))]
              (if (string=? name to)
                  npath
                  (fn-for-lot subs npath))))
          
          (define (fn-for-lot lot path)
            (cond [(empty? lot) false]
                  [else
                   (local [(define try (fn-for-t (first lot) path))]
                     (if (not (false? try))
                         try
                         (fn-for-lot (rest lot) path)))]))]
    
    (fn-for-t t empty)))

;;
;; PROBLEM
;;
;; Define a new version of the function which is tail recursive.  You only
;; need a new function definition. Proceed by
;;   - copying your solution to the problem above
;;   - and renaming the path accumulator to visited
;;   - then convert the code to be tail recursive
;;



(@template backtracking Tree (listof Tree) accumulator)

;; Tail recursion with visited accumulator. Fails some tests.
(define (find-path t to)
  ;; visited is (listof String); names of trees visited so far
  ;; todo is (listof Tree); worklist
  (local [(define (fn-for-t t todo visited)
            (local [(define name (node-name t))
                    (define subs (node-subs t))
                    (define nvisited (append visited (list name)))]
              (if (string=? name to)
                  nvisited
                  (fn-for-lot (append subs todo) nvisited))))
          
          (define (fn-for-lot todo visited)
            (cond [(empty? todo) false]
                  [else
                   (fn-for-t (first todo) (rest todo) visited)]))]
    
    (fn-for-t t empty empty)))